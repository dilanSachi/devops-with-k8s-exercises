plugins {
    id 'java'
}

group = 'fi.devopswithk8s.exercises'
version = '1.0-SNAPSHOT'
def dockerImageName = project.getProperty("image_tag")

repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes 'Main-Class': 'fi.devopswithk8s.exercises.BroadcasterApp'
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }

}

dependencies {
    implementation group: 'io.nats', name: 'jnats', version: '2.24.1'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

task buildDockerImage(type: Exec) {
    workingDir '.'
    commandLine 'docker', 'build', '-t', dockerImageName, '.'
}

task importDockerImageToK3d() {
    doLast {
        if (project.getProperty("skip_local_image_build") == "0") {
            def checkK3d = "k3d --version".execute()
            checkK3d.waitFor()
            if (checkK3d.exitValue() != 0) {
                println "k3d is not installed or not in PATH. Hence skipping image import."
                return
            }
            def importCmd = "k3d image import ${dockerImageName}".execute()
            importCmd.waitForProcessOutput(System.out, System.err)
        }
    }
}

task uploadDockerImageToDockerHub() {
    doLast {
        if (project.getProperty("skip_local_image_build") == "0") {
            def pushImage = "docker push ${dockerImageName}".execute()
            pushImage.waitForProcessOutput(System.out, System.err)
            if (pushImage.exitValue() != 0) {
                println "Failed to push docker image: ${dockerImageName} to hub."
                return
            }
            println "Pushed docker image: ${dockerImageName} to hub successfully."
        }
    }
}

importDockerImageToK3d.dependsOn buildDockerImage
uploadDockerImageToDockerHub.dependsOn buildDockerImage
build.finalizedBy importDockerImageToK3d
build.finalizedBy uploadDockerImageToDockerHub
